apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.github.featheredtoast"
archivesBaseName = "javad"
version = "0.2.1"

repositories {
    jcenter()
}

dependencies {
  compile 'org.slf4j:slf4j-api:1.7.18',
  'log4j:log4j:1.2.17',
  'commons-logging:commons-logging:1.1.1'
  testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Javad'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'Java library for loading properties files from a directory and watching for changes'
        url 'https://github.com/featheredtoast/javad'

        scm {
          connection 'scm:git:https://github.com/featheredtoast/javad.git'
          developerConnection 'scm:git:https://github.com/featheredtoast/javad.git'
          url 'https://github.com/featheredtoast/javad'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'jwong'
            name 'Jeff Wong'
            email 'awole20@gmail.com'
          }
        }
      }
    }
  }
}
